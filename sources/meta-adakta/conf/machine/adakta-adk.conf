#@TYPE: Machine
#@NAME: ADAKTA BSP for IMX6
#@SOC: i.MX6 Q/DL
#@DESCRIPTION: Machine configuration for Adakta ADK
#@MAINTAINER: Ilya Gorchakov <ilya.gorchakov@adakta.ru>
#
#
# By default, this machine will build for the standard Quad-Core, 1GB
# option.
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor           Memory    Configuration
# --------------      ------    --------------------
# i.MX6Quad/Dual        1GB     adakta_qd1g_config
# i.MX6Quad/Dual        2GB     adakta_qd2g_config
# i.MX6Quad/Dual        4GB     adakta_qd4g_config
# i.MX6Dual-Lite        1GB     adakta_dl1g_config
# i.MX6Dual-Lite        2GB     adakta_dl2g_config
# i.MX6Solo           512MB     adakta_s0g_config
# i.MX6Solo             1GB     adakta_s1g_config
#
#
#

MACHINEOVERRIDES =. "mx6:mx6dl:mx6q:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc


KERNEL_DEVICETREE_adkstand = "imx6qp-adk_stand.dtb imx6qd-adk_stand.dtb imx6dls-adk_stand.dtb"
KERNEL_DEVICETREE_adk = "imx6qp-adk.dtb imx6qd-adk.dtb imx6dls-adk.dtb"

KERNEL_DEVICETREE = " \
${@bb.utils.contains_any("KERNEL_DEFCONFIG_TYPE", "adk", "${KERNEL_DEVICETREE_adk}" , "", d)} \
${@bb.utils.contains_any("KERNEL_DEFCONFIG_TYPE", "adkstand", "${KERNEL_DEVICETREE_adkstand}" , "", d)} \
"

KERNEL_IMAGETYPE = "zImage"

PREFERRED_PROVIDER_u-boot = "u-boot-adakta"
PREFERRED_PROVIDER_virtual/kernel ??= "linux-adk"

PREFERRED_PROVIDER_virtual/kernel_use-mainline-bsp ??= "linux-fslc"

# Use SPI NOR U-Boot by default
IMAGE_BOOTLOADER ?= ""

# Ensure boot scripts will be available at rootfs time
do_rootfs[depends] += "u-boot-script-adakta:do_deploy"

# Boot scripts to install
BOOT_SCRIPTS = "6x_bootscript-${MACHINE}:6x_bootscript"

IMAGE_BOOT_FILES = " \
    6x_bootscript-${MACHINE};6x_bootscript \
    ${KERNEL_IMAGETYPE} \
    ${KERNEL_IMAGETYPE}-imx6qp-adk.dtb;imx6qp-adk.dtb \
    ${KERNEL_IMAGETYPE}-imx6qd-adk.dtb;imx6qd-adk.dtb \
    ${KERNEL_IMAGETYPE}-imx6dls-adk.dtb;imx6dls-adk.dtb \ 
"

UBOOT_MACHINE ?= "imx6q-adk_config"

SERIAL_CONSOLE = "115200 ttymxc1"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += " \
    kernel-image \
    kernel-devicetree \
"

MACHINE_FEATURES += " pci"
